10 REM --- Epson HX-20      ---
20 REM --- M. Hepperle 2024 ---
30 REM --- adjust BASIC starting address
40 MEMSET &HAEF
50 REM --- install the device "BUF0:"
60 GOSUB 350
70 REM --- application example
75 PRINT "1) Writing"
80 OPEN "O",#1,"BUF0:"
90 PRINT #1,"123";
100 PRINT "123 POS=";POS(1)
110 PRINT #1,"ABC"
120 PRINT #1,"abcd";
130 PRINT "ABC;abcd POS=";POS(1)
140 CLOSE #1
145 PRINT "2) Reading"
150 ON ERROR GOTO 240
160 OPEN "I",#1,"BUF0:"
170 PRINT "Buffer size=";LOF(1)
180 FOR I%=1 TO 100
190  C$=INPUT$(1,#1)
200  IF ASC(C$)=13 THEN C$="CR"
210  IF ASC(C$)=10 THEN C$="LF"
220  PRINT "/";C$;
230 NEXT I%
240 CLOSE #1
250 PRINT"/"
260 PRINT "UNLINK BUF0:"
270 INPUT "Y/N";YN$
280 IF YN$<>"Y" THEN GOTO 330
290 IF A%=0 THEN GOTO 330
300 POKE A%,0
310 POKE A%+1,0
320 PRINT "BUF0: @";A%;"removed"
330 END
340 REM --- Hex Code Loader  ---
350 N%=0
360 READ C$
370 IF C$="DONE" THEN 420
380 N%=N%+1 : IF N%=8 THEN PRINT "."; : N%=0
390 C%=VAL("&H"+C$)
400 IF LEN(C$)=4 THEN A%=C% : GOTO 360
410 POKE A%,C% : A%=A%+1 : GOTO 360
420 PRINT
430 REM install device in DCB table
440 DCBTAB%=&H0657
450 FOR A%=DCBTAB% TO DCBTAB%+30 STEP 2
460  C%=PEEK(A%)*256+PEEK(A%+1)
470  IF C%=&H0A40 THEN GOTO 530 : REM already there
480  IF C%=&H0000 THEN GOTO 510 : REM empty
490 NEXT A%
500 IF A%>DCBTAB%+28 THEN GOTO 550
510 POKE A%,&H0A : REM HIGH
520 POKE A%+1,&H40 : REM LOW
530 PRINT "BUF0: at";A%;"added"
540 RETURN
550 PRINT "Cannot install BUF0:"
560 STOP
570 DATA 0A40,42,55,46,30,30,0A,5E,0A,5F,0A,60,0A,7C,0A,A0,0A,A3,00
580 DATA 00,00,00,00,00,14,00,80,0A,AF,0A,AF,39,39,FE,0A,5A,BC,0A,5C
590 DATA 27,0F,A6,00,08,8C,0A,EF,26,03,CE,0A,AF,FF,0A,5A,39,86,FF,97
600 DATA F5,39,5F,D7,F5,FE,0A,5C,A7,00,08,8C,0A,EF,26,03,CE,0A,AF,FF
610 DATA 0A,5C,81,0D,27,08,81,0A,27,04,7C,0A,55,39,7F,0A,55,39,D6,F5
620 DATA 39,FC,0A,5C,B3,0A,5A,2A,03,C3,00,40,39,DONE
630 REM --- END
